# OME/Java/DTOInterface.pm

#-------------------------------------------------------------------------------
#
# Copyright (C) 2003 Open Microscopy Environment
#       Massachusetts Institute of Technology,
#       National Institutes of Health,
#       University of Dundee
#
#
#
#    This library is free software; you can redistribute it and/or
#    modify it under the terms of the GNU Lesser General Public
#    License as published by the Free Software Foundation; either
#    version 2.1 of the License, or (at your option) any later version.
#
#    This library is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
#    Lesser General Public License for more details.
#
#    You should have received a copy of the GNU Lesser General Public
#    License along with this library; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
#-------------------------------------------------------------------------------




#-------------------------------------------------------------------------------
#
# Written by:    Douglas Creager <dcreager@alum.mit.edu>
#
#-------------------------------------------------------------------------------


package OME::Java::DTOInterface;

=head1 NAME

OME::Java::DTOInterface

=cut

use strict;
use OME;
our $VERSION = $OME::VERSION;

use File::Spec;
use File::Path;
use IO::Handle;
use IO::File;

=head1 SYNOPSIS

	OME::Java::DTOInterface->
	    writeInterface(\%interface_description,%options);

=head1 DESCRIPTION

This class contains methods for auto-generating Java interfaces for
OME semantic types.  Java client code can then be written and compiled
against these interfaces.  B<TODO>: We will include in the new remote
interface the ability to use Java dynamic proxies to automatically
generate data objects which conform to these interfaces.

=head1 SEMANTIC TYPE INTERFACES

The Java interface generated by this class is very straightforward.
It will contain accessor and mutator methods for each of the elements
defined by the semantic type.  The names of the elements should be
standard alphanumeric characters, but if they do contain other
characters, those will be mapped into underscores.

Convention dictates that semantic type and element names be
hypercapitalized (e.g., Name, Address, PhoneNumber).  Since this is
the same convention for Java class names, the interface will be named
C<org.openmicroscopy.st.I<[semantic type name]>>.  It also allows the
method names to be formed trivially by prepending "get", "set", or
"is" as appropriate.  If the element is of boolean type, any leading
"Is" is stripped from the name (to prevent accessors named, for
instance, "isIsLocal").

=head2 Semantic type conflicts

There is, of course, the possibility that two different OME
installations will define logically distinct semantic types with the
same name.  Unfortunately, that means that the Java interfaces created
by this class will have the same name, but different definitions.

Code written against one of the interfaces will compile correctly, but
might generate incorrect results when connected to an OME database
with a different definition of the corresponding semantic type.

(B<NOTE>: This list is hypothetical, and based on a presumed
Map-backed view proxy implementation of the ST interfaces.)

=over

=item 1.

Elements in the Java interface but not in the semantic type would seem
to be populated with null values when read from the database.  Changes
to these elements would work within the context of the active Java
application (since it would just be modifying an in-memory Map), but
an error would be generated if the code tried to write these changes
back to the database.  (The underlying DBObject implementation would
complain about the unknown keys in the hash.)

=item 2.

For elements that happened to have the same name and type in both ST
definitions, both the accessors and mutators would work correctly.

=item 3.

Elements in the semantic type but not in the Java interface would be
inaccessible from the Java code.

=back

Luckily, the XML representation of a semantic type has an LSID, which
is guaranteed to be unique.  Therefore, the writeInterfaces method
will create additional Java interfaces for each LSID.  An ST's LSID
has the form

	urn:lsid:[LSID authority]:SemanticType:[number]:[db instance]

The LSID authority looks like a hostname, and has the same function as
a package name in Java -- it just defines a namespace.  The number is
supposed to be a positive integer unique within the purview of the
LSID authority.  The database instance is an alphanumeric string that
is supposed to be unique between different OME installations on the
same computer.  Given this, each LSID corresponds to a unique Java
fully-qualified name (FQN):

	org.openmicroscopy.st.lsid.[LSID authority].db[db instance].LSID[number]

Therefore, the writeInterfaces method will create two interfaces for
each semantic type -- one for its LSID (which declares no methods),
and one for the type itself (which declares methods for its elements,
as described above).  The semantic type interface is declared to be a
subinterface of the LSID interface.

This setup allows Java client code to worry about the uniqueness of
semantic types only as necessary.  If desired, the code can be written
to the semantic type interfaces, without worrying about type
conflicts.  If the code happens to connect to a conflicting OME
installation, the code (or the user) must be prepared to deal with the
error conditions described above.

On the other hand, if the code is intended to connect to widely
varying OME installations, the Java code can use the standard Java
type-checking operator to enforce the use of a specific semantic type.
For instance, you can verify that an attribute is an instance of the
core Repository semantic type (and not a different type which happens
to also be named "Repository") with the following code:

	import org.openmicroscopy.st.Repository;
	import org.openmicroscopy.st.lsid.openmicroscopy.org.LSID1;

	[ snip ]

	Repository repository = (Repository) factory.
	    loadAttribute(Repository.class,1);

	if (!repostory instanceof LSID1)
	    throw new Exception("Conflicting semantic types!");

=cut

my $options;
my $username = getpwuid($<);
my $now;

sub openHandle ($$) {
    my ($subdir,$filename) = @_;
    my $fh;

    if (exists $options->{OutputDirectory}) {
        my $outdir = $options->{OutputDirectory};
        my $fulldir = File::Spec->catdir($outdir,$subdir);
        mkpath($fulldir);
        my $path = File::Spec->catfile($fulldir,$filename);
        $fh = IO::File->new($path,'w');
    } else {
        $fh = IO::Handle->new_from_fd('STDOUT','a');
        my $path = File::Spec->catfile($subdir,$filename);
        print $fh "\n*****\n$path\n\n";
    }

    return $fh;
}

sub getDirectoryForPackage ($) {
    my ($package) = @_;
    $package =~ s:\.:/:g;
    return $package;
}

sub writePreamble ($$) {
    my ($fh,$classname) = @_;

    print $fh <<"JAVA";
/*
 * $classname
 *
 *------------------------------------------------------------------------------
 *
 *  Copyright (C) 2003-2004 Open Microscopy Environment
 *      Massachusetts Institute of Technology,
 *      National Institutes of Health,
 *      University of Dundee
 *
 *
 *
 *    This library is free software; you can redistribute it and/or
 *    modify it under the terms of the GNU Lesser General Public
 *    License as published by the Free Software Foundation; either
 *    version 2.1 of the License, or (at your option) any later version.
 *
 *    This library is distributed in the hope that it will be useful,
 *    but WITHOUT ANY WARRANTY; without even the implied warranty of
 *    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 *    Lesser General Public License for more details.
 *
 *    You should have received a copy of the GNU Lesser General Public
 *    License along with this library; if not, write to the Free Software
 *    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 *
 *------------------------------------------------------------------------------
 */

/*------------------------------------------------------------------------------
 *
 * THIS IS AUTOMATICALLY GENERATED CODE.  DO NOT MODIFY.
 * Created by $username via omejava on $now
 *
 *------------------------------------------------------------------------------
 */

JAVA
}

my %MDTO_ACCESSORS = (
                      boolean => 'getBooleanElement',
                      String  => 'getStringElement',
                      float   => 'getFloatElement',
                      double  => 'getDoubleElement',
                      int     => 'getIntElement',
                      long    => 'getLongElement',
                     );

my %MDTO_VALUES = (
                   boolean => 'new Boolean(value)',
                   String  => 'value',
                   float   => 'new Float(value)',
                   double  => 'new Double(value)',
                   int     => 'new Integer(value)',
                   long    => 'new Long(value)',
                  );

sub writeOneAccessor ($$$$$) {
    my ($int_fh,$dto_fh,$map_code,$field_name,$field_desc) = @_;

    my $java_type = $field_desc->[0];
    my $element_name = $field_desc->[1];
    $element_name = lc($field_name)
      unless defined $element_name && $element_name ne "";

    my $prefix = $java_type eq 'boolean'? 'is': 'get';
    my $accessor;
    my $value;
    if ($java_type =~ /^List\:(.*)$/) {
        my $list_type = $1;
        $java_type = "List";
        $accessor = "(List) getObjectElement";
        $value = "value";

        $$map_code .= <<"JAVA";
        parseListElement("${element_name}",${list_type}DTO.class);
JAVA
    } elsif (exists $MDTO_ACCESSORS{$java_type}) {
        $accessor = $MDTO_ACCESSORS{$java_type};
        $value = $MDTO_VALUES{$java_type};
    } else {
        $accessor = "(${java_type}) getObjectElement";
        $value = "value";

        $$map_code .= <<"JAVA";
        parseChildElement("${element_name}",${java_type}DTO.class);
JAVA
    }

    print $int_fh <<"JAVA";
    public ${java_type} ${prefix}${field_name}();
JAVA

    print $dto_fh <<"JAVA";
    public ${java_type} ${prefix}${field_name}()
    { return ${accessor}("${element_name}"); }
JAVA

    if ($java_type ne 'List') {
    print $int_fh <<"JAVA";
    public void set${field_name}(${java_type} value);
JAVA

        print $dto_fh <<"JAVA";
    public void set${field_name}(${java_type} value)
    { setElement("${element_name}",${value}); }
JAVA
    }

    print $int_fh "\n";
    print $dto_fh "\n";
}

sub writeOneClass ($$) {
    my $class = shift;
    $options = shift;
    $now = localtime();

    # Generate the package, class, and file names for the type interface
    my $main_pkg = $class->{Package};
    my $main_pkg_dir = getDirectoryForPackage($main_pkg);

    # Generate the package, class, and file names for the type interface
    my $int_class = $class->{Class};
    my $int_fqn = "${main_pkg}.${int_class}";
    my $int_filename = "${int_class}.java";

    # Generate the package, class, and file names for the DTO
    my $dto_class = "${int_class}DTO";
    my $dto_fqn = "${main_pkg}.${dto_class}";
    my $dto_filename = "${dto_class}.java";
 
    # Output the type interface and DTO

    my $int_fh = openHandle($main_pkg_dir,$int_filename);
    writePreamble($int_fh,$int_fqn);
    print $int_fh "package ${main_pkg};\n\n";

    my $dto_fh = openHandle($main_pkg_dir,$dto_filename);
    writePreamble($dto_fh,$dto_fqn);
    print $dto_fh "package ${main_pkg};\n\n";

    # Add any specified import clauses

    if (defined $class->{ImportPackages}) {
        my $imports = $class->{ImportPackages};

        foreach my $import_pkg (sort keys %$imports) {
            my $import = $imports->{$import_pkg};

            if (ref($import) eq 'ARRAY') {
                foreach my $import_class (@$import) {
                    print $int_fh "import ${import_pkg}.${import_class};\n";
                    print $dto_fh "import ${import_pkg}.${import_class};\n";
                }
            } elsif (!ref($import)) {
                print $int_fh "import ${import_pkg}.${import};\n";
                print $dto_fh "import ${import_pkg}.${import};\n";
            }
        }
    }

    # Output the interface specification

    print $int_fh <<"JAVA";
import java.util.List;
import java.util.Map;

public interface ${int_class}
JAVA

    if (defined $class->{Superinterfaces}) {
        my $extends = $class->{Superinterfaces};
        my $extend_string;
        if (ref($extends) eq 'ARRAY') {
            $extend_string = join(', ',@$extends);
        } elsif (!ref($extends)) {
            $extend_string = $extends;
        }

        print $int_fh "    extends $extend_string\n";
    }

    print $int_fh "{\n";

    if (!defined $class->{Superclass}) {
        print $dto_fh <<"JAVA";
import org.openmicroscopy.ds.dto.MappedDTO;
import java.util.List;
import java.util.Map;

public class ${dto_class}
    extends MappedDTO
    implements ${int_class}
{
    public ${dto_class}() { super(); }
    public ${dto_class}(Map elements) { super(elements); }

JAVA
    } else {
        my $superclass_spec = $class->{Superclass};
        my ($superpackage,$superclass) = @$superclass_spec;

        print $dto_fh <<"JAVA";
import ${superpackage}.${superclass};
import java.util.List;
import java.util.Map;

public class ${dto_class}
    extends ${superclass}
    implements ${int_class}
{
    public ${dto_class}() { super(); }
    public ${dto_class}(Map elements) { super(elements); }

JAVA
    }

    # Output an accessor and mutator for each semantic element

    my $map_code = "";

    my $fields = $class->{Fields};

    while (my ($field_name,$field_desc) = splice(@$fields,0,2)) {
        writeOneAccessor($int_fh,$dto_fh,\$map_code,
                         $field_name,$field_desc);
    }

    # Output the interface's closing brace

    print $int_fh <<"JAVA";
}
JAVA

    if ($map_code ne "") {
        print $dto_fh <<"JAVA";
    protected void setMap(Map elements)
    {
        super.setMap(elements);
$map_code    }

}
JAVA
    } else {
        print $dto_fh "\n}\n";
    }

    $int_fh->close();
    $dto_fh->close();

}

1;

__END__

=head1 AUTHOR

Douglas Creager <dcreager@alum.mit.edu>,
Open Microscopy Environment, MIT

=head1 SEE ALSO

L<OME>, http://www.openmicroscopy.org/

=cut


